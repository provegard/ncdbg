plugins {
    id "com.github.maiflai.scalatest" version "0.22"
    id 'com.palantir.git-version' version '0.5.3'
}

group 'com.programmaticallyspeaking'
version gitVersion()

apply plugin: 'scala'
apply plugin: 'application'

def toolsJar = org.gradle.internal.jvm.Jvm.current().toolsJar

sourceCompatibility = 1.8

mainClassName = "com.programmaticallyspeaking.ncd.boot.Boot"

def generatedResourcesFolder = "$buildDir/generated-resources"

// For the application plugin
applicationName = "ncdbg"

sourceSets {
    main {
        output.dir(generatedResourcesFolder, builtBy: 'generateResources')
    }
}

task generateResources {
    doLast {
        mkdir generatedResourcesFolder
        def buildProps = new File(generatedResourcesFolder, "build.properties")
        def javaVer = org.gradle.internal.jvm.Jvm.current().javaVersion
        buildProps.text = """version=$version
build_java_version=$javaVer
"""
    }
}

test {
    // Always run tests!
    outputs.upToDateWhen {false}

    systemProperty "SelectiveActorLogging.suppress", "true"
    systemProperty "logback.configurationFile", "logback-off.xml"

    // Don't run tests in parallel. Since we listen for log events from MemoryAppender to include in the failure output,
    // running tests in parallel results in too much noise.
    maxParallelForks = 1
}

run {
    // Useful for testing command-line argument parsing using "gradle run -Pargs='...'"
    if (project.hasProperty('args')){
        args project.args.split('\\s+')
    }
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    def logbackVersion = '1.1.7'
    def akkaHttpVersion = '10.0.3'
    def akkaVersion = '2.4.17'

    // needed for gradle-scalatest
    testRuntime 'org.pegdown:pegdown:1.6.0'

    testCompile group: 'org.scalatest', name: 'scalatest_2.12', version: '3.0.1'
    testCompile group: 'org.scalacheck', name: 'scalacheck_2.12', version: '1.13.4'

    testCompile group: 'com.typesafe.akka', name: 'akka-http-testkit_2.12', version: akkaHttpVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.2.27'
    testCompile group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.3.0'

    testCompile files("lib/script.jar")

    if (toolsJar != null) {
        compile files(toolsJar)
    }

    compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion

    compile group: 'com.programmaticallyspeaking', name: 'tinyws', version: '0.0.6'

    compile group: 'org.slf4s', name: 'slf4s-api_2.12', version: '1.7.25'

    compile group: 'com.typesafe.akka', name: 'akka-actor_2.12', version: akkaVersion

    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-scala_2.12', version: '2.9.6'

    compile 'org.scala-lang:scala-library:2.12.6'

    compile group: 'org.rogach', name: 'scallop_2.12', version: '2.1.0'

    compile group: 'com.google.javascript', name: 'closure-compiler', version: 'v20180204'
}

// Exclude tools.jar from the distribution since we want to use the one from the user's JDK.
distZip {
    excludes += ['**/tools.jar']
}

// Excluding tools.jar doesn't remove it from the start script, where it's assumed to be in the local lib folder.
// Do some text replacement to change its location to lib under JAVA_HOME instead.
startScripts {
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        if (toolsJar != null) {
            windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\tools.jar', '%JAVA_HOME%\\lib\\tools.jar')
            // If JAVA_HOME is a Windows path (e.g. under MinGW), we need to remove the colon (since that's the class path
            // separator) and prefix with a slash to make Java understand that it's a path. This has been tested in
            // Windows with Git Bash (MINGW64) and in Linux.
            // Note: Variable substitution doesn't work with dash, which is the sh replacement on Linux,
            // hence the use of echo + tr.
            unixScriptFile.text = unixScriptFile.text.replace('$APP_HOME/lib/tools.jar', '/$(echo $JAVA_HOME | tr -d ":")/lib/tools.jar')
        } else {
            // Java distribution used for building doesn't include tools.jar, so append it to CLASSPATH
            windowsScriptFile.text = windowsScriptFile.text.replaceFirst('(CLASSPATH=.*)', '$1;%JAVA_HOME%\\\\lib\\\\tools.jar')
            unixScriptFile.text = unixScriptFile.text.replaceFirst('(CLASSPATH=.*)', '$1:/\\$\\(echo \\$JAVA_HOME | tr -d ":"\\)/lib/tools.jar')
        }
    }
}
